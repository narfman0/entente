apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "application"
apply plugin: 'maven'

group = 'com.blastedstudios'
mainClassName = "com.blastedstudios.entente.Host"
version = '1.0.0'
description = "A java - google protocol buffers based communication library"
defaultTasks 'eclipse', 'build', 'install', 'javadoc', 'createPom'

ext {
    appName = "entente"
    url = "https://github.com/narfman0/entente"
}

jar {
    manifest {
        attributes 'Main-Class': '$mainClassName'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocsJar
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.0.0'
    testCompile 'junit:junit:4.12'
}

task createPom {
    pom {
        project {
		    name "${appName}";
		    description "${description}";
		    url "${url}";
            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
            	developer {
            		name 'Jon Robison'
            		email 'narfman0@gmail.com'
            	}
            }
            scm {
            	connection "scm:git:${url}.git"
            	developerConnection "scm:git:${url}.git"
            	url "${url}"
            }
        }
    }.writeTo("$buildDir/poms/pom.xml")
}